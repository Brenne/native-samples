import groovy.io.FileType
import org.eclipse.jgit.api.Git
import org.eclipse.jgit.api.InitCommand

buildscript {
    dependencies {
        classpath "org.eclipse.jgit:org.eclipse.jgit:4.9.1.201712030800-r"
    }
}

plugins {
    id "groovy"
    id "idea"
}

repositories {
    jcenter()
}

dependencies {
    testImplementation gradleTestKit()
    testImplementation localGroovy()
    testImplementation("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
    testImplementation "com.atlassian.commonmark:commonmark:0.10.0"
}

test {
    inputs.files files("../swift", '../cpp').asFileTree.matching { PatternFilterable pattern ->
        pattern.exclude "**/build", "**/.gradle", "**/*.xcworkspace", "**/*.xcodeproj", "**/repo"
    }
}

task cleanRepo(type: Delete) {
    delete file("../cpp/repo")
}

task cleanSamples {
    dependsOn cleanRepo
    doLast {
        def toDelete = [
            file('../cpp/source-dependencies/repos'),
            file('../swift/source-dependencies/repos'),
            file('../swift/swift-package-manager-publish/release-plugin'),
            file('../swift/swift-package-manager-publish/list-library'),
            file('../swift/swift-package-manager-publish/utilities-library')
        ]
        [file("../swift"), file("../cpp")]*.eachFileRecurse(FileType.DIRECTORIES) { def dir ->
            if ([/repo/, /build/, /\.gradle/, /\.build/, /.+\.xcworkspace/, /.+\.xcodeproj/].any { dir.name ==~ it }) {
                toDelete << dir
            }
        }
        toDelete.each { it.deleteDir() }
    }
}

clean.dependsOn cleanSamples, cleanRepo

task configureNightlyWrapper {
    doLast {
        configure(tasks.nightlyWrapper) {
            def version = new groovy.json.JsonSlurper().parseText(new URL("https://services.gradle.org/versions/nightly").text)
            if (version.empty) {
                throw new GradleException("Cannot update wrapper to 'nightly' version as there is currently no version of that label")
            }
            println "updating wrapper to nightly version: $version.version (downloadUrl: $version.downloadUrl)"
            distributionUrl version.downloadUrl
        }
    }
}

task nightlyWrapper(type: Wrapper) {
    dependsOn configureNightlyWrapper
    group = "wrapper"
    description = "Updates the samples to use the most recent nightly"
    scriptFile = "../gradlew"
    jarFile = "../gradle/wrapper/gradle-wrapper.jar"
}

tasks.withType(SourceCopyTask) {
    templatesDir = file("src/templates")
    group = "source generation"
}

task generateSource {
    dependsOn tasks.withType(SourceCopyTask)
    group = "source generation"
    description = "generate the source files for all samples"
}

task generateRepos {
    dependsOn tasks.withType(GitRepoTask)
    group = "sample setup"
    description = "generate the Git repositories for all samples"
}

def cppUtilsLib = CppLibraryTemplate.of("cpp-lib-with-api-dep", "utilities")
def cppListLib = CppLibraryTemplate.of("cpp-lib", "list")

def swiftUtilsLib = SwiftLibraryTemplate.of("swift-lib-with-api-dep", "Utilities")
def swiftListLib = SwiftLibraryTemplate.of("swift-lib", "List")

task cppTransitiveDependencies(type: SourceCopyTask) {
    sampleDir = file("../cpp/transitive-dependencies")

    project(".").buildRoot()
    appProject("app").fromTemplate("cpp-app")
    libProject("utilities").fromTemplate(cppUtilsLib)
    libProject("list").fromTemplate(cppListLib)
}

task cppApplication(type: SourceCopyTask) {
    sampleDir = file("../cpp/application")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("cpp-app")
    appProject(".").fromTemplate(cppUtilsLib)
    appProject(".").fromTemplate(cppListLib)
}

task cppLibrary(type: SourceCopyTask) {
    sampleDir = file("../cpp/simple-library")

    libProject(".").buildRoot()
    libProject(".").fromTemplate(cppListLib)
}

task cppBinaryDependencies(type: SourceCopyTask) {
    sampleDir = file("../cpp/binary-dependencies")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("cpp-app")
    appProject(".").fromTemplate(cppUtilsLib)
}

task cppPrebuiltBinaries(type: SourceCopyTask) {
    sampleDir = file("../cpp/prebuilt-binaries")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("cpp-app")
    appProject(".").fromTemplate(cppUtilsLib)
}

task cppCompositeBuild(type: SourceCopyTask) {
    sampleDir = file("../cpp/composite-build")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("cpp-app")
    libProject("utilities-library").fromTemplate(cppUtilsLib)
    libProject("list-library").fromTemplate(cppListLib)
}

task cppSourceDependencies(type: SourceCopyTask) {
    sampleDir = file("../cpp/source-dependencies")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("cpp-app")
    libProject("repos/utilities-library").fromTemplate(cppUtilsLib)
    libProject("repos/utilities-library").fromTemplate("cpp-utilities-lib-build")
    libProject("repos/list-library").fromTemplate(cppListLib)
    libProject("repos/list-library").fromTemplate("cpp-list-lib-build")
}

task cppOSDependencies(type: SourceCopyTask) {
    sampleDir = file("../cpp/operating-system-specific-dependencies")

    project(".").buildRoot()
    appProject("app").fromTemplate("cpp-app-uses-logger")
    appProject("app").fromTemplate("cpp-logger-uses-console")
    appProject("app").fromTemplate(cppUtilsLib)
    appProject("app").fromTemplate(cppListLib)
    libProject("ansiConsole").fromTemplate(CppLibraryTemplate.of("cpp-ansi-console-lib", "ansi_console"))
    libProject("winConsole").fromTemplate(CppLibraryTemplate.of("cpp-windows-console-lib", "win_console"))
}

task cppSourceGeneration(type: SourceCopyTask) {
    sampleDir = file("../cpp/source-generation")

    project(".").buildRoot()
}

task cppStaticLibrary(type: SourceCopyTask) {
    sampleDir = file("../cpp/static-library")

    project(".").buildRoot()
    appProject("app").fromTemplate("cpp-app")
    staticLibProject("utilities").fromTemplate(cppUtilsLib)
    libProject("list").fromTemplate(cppListLib)
}

task cppSwiftPackageManager(type: SourceCopyTask) {
    sampleDir = file("../cpp/swift-package-manager")

    project(".").buildRoot()
    swiftPMProject(".", "App").fromTemplate("cpp-app")
    swiftPMProject(".", "Utilities").fromTemplate(cppUtilsLib)
    swiftPMProject(".", "List").fromTemplate(cppListLib)
}

task swiftApplication(type: SourceCopyTask) {
    sampleDir = file("../swift/application")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("swift-app")
    appProject(".").fromTemplate(swiftUtilsLib)
    appProject(".").fromTemplate(swiftListLib)
}

task swiftApplicationOSDependencies(type: SourceCopyTask) {
    sampleDir = file("../swift/operating-system-specific-dependencies")

    project(".").buildRoot()
    appProject("app").fromTemplate("swift-app")
    appProject("app").fromTemplate("swift-app-uses-logger")
    appProject("app").fromTemplate("swift-logger-uses-console")
    appProject("app").fromTemplate(swiftUtilsLib)
    appProject("app").fromTemplate(swiftListLib)

    libProject("linux-console").fromTemplate(SwiftLibraryTemplate.of("swift-linux-console-lib", "LinuxLogger"))
    libProject("macos-console").fromTemplate(SwiftLibraryTemplate.of("swift-macos-console-lib", "MacOsLogger"))
}

task swiftVersions(type: SourceCopyTask) {
    sampleDir = file("../swift/swift-versions")

    project(".").buildRoot()

    appProject("swift3-app").fromTemplate("swift-app")
    appProject("swift3-app").fromTemplate("swift-app-uses-logger")
    appProject("swift3-app").fromTemplate(SwiftLibraryTemplate.of("swift-logger-reports-swift-version", "Logger"))
    appProject("swift3-app").fromTemplate(SwiftLibraryTemplate.of("swift3-lib-with-api-dep", "Utilities"))
    appProject("swift3-app").fromTemplate(swiftListLib)

    appProject("swift4-app").fromTemplate("swift-app")
    appProject("swift4-app").fromTemplate("swift-app-uses-logger")
    appProject("swift4-app").fromTemplate(SwiftLibraryTemplate.of("swift-logger-reports-swift-version", "Logger"))
    appProject("swift4-app").fromTemplate(SwiftLibraryTemplate.of("swift4-lib-with-api-dep", "Utilities"))
    appProject("swift4-app").fromTemplate(swiftListLib)
}

task swiftSimpleLibrary(type: SourceCopyTask) {
    sampleDir = file("../swift/simple-library")

    libProject(".").buildRoot()
    libProject(".").fromTemplate(swiftListLib)
}

task swiftPrebuiltBinaries(type: SourceCopyTask) {
    sampleDir = file("../swift/prebuilt-binaries")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("swift-app")
    appProject(".").fromTemplate(swiftUtilsLib)
}

task swiftTransitiveDependencies(type: SourceCopyTask) {
    sampleDir = file("../swift/transitive-dependencies")

    project(".").buildRoot()
    appProject("app").fromTemplate("swift-app")
    libProject("utilities").fromTemplate(swiftUtilsLib)
    libProject("list").fromTemplate(swiftListLib)
}

task swiftCompositeBuild(type: SourceCopyTask) {
    sampleDir = file("../swift/composite-build")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("swift-app")
    libProject("utilities-library").fromTemplate(swiftUtilsLib)
    libProject("list-library").fromTemplate(swiftListLib)
}

task swiftInjectedPlugins(type: SourceCopyTask) {
    sampleDir = file("../swift/injected-plugins")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("swift-app")
    libProject("repos/utilities-library").fromTemplate(swiftUtilsLib)
    libProject("repos/list-library").fromTemplate(SwiftLibraryTemplate.of("swift-lib-unusable", "List"))
}

task swiftSourceDependencies(type: SourceCopyTask) {
    sampleDir = file("../swift/source-dependencies")

    appProject(".").buildRoot()
    appProject(".").fromTemplate("swift-app")
    libProject("repos/utilities-library").fromTemplate(swiftUtilsLib)
    libProject("repos/utilities-library").fromTemplate("swift-utilities-lib-build")
    libProject("repos/list-library").fromTemplate(swiftListLib)
    libProject("repos/list-library").fromTemplate("swift-list-lib-build")
}

task swiftSourceGeneration(type: SourceCopyTask) {
    sampleDir = file("../swift/source-generation")

    project(".").buildRoot()
}

task swiftStaticLibrary(type: SourceCopyTask) {
    sampleDir = file("../swift/static-library")

    project(".").buildRoot()
    appProject("app").fromTemplate("swift-app")
    libProject("utilities").fromTemplate(swiftUtilsLib)
    libProject("list").fromTemplate(swiftListLib)
}

task swiftCppDependencies(type: SourceCopyTask) {
    sampleDir = file("../swift/cpp-dependencies")

    project(".").buildRoot()
    staticLibProject("list").fromTemplate("cpp-lib-with-c-api")
    appProject("app").fromTemplate(SwiftLibraryTemplate.of("swift-lib-uses-c-api", "List"))
    appProject("app").fromTemplate(swiftUtilsLib)
    appProject("app").fromTemplate("swift-app")
}

task swiftSwiftPackageManager(type: SourceCopyTask) {
    sampleDir = file("../swift/swift-package-manager")

    project(".").buildRoot()
    swiftPMProject(".", "App").fromTemplate("swift-app")
    swiftPMProject(".", "Utilities").fromTemplate(swiftUtilsLib)
    swiftPMProject(".", "List").fromTemplate(swiftListLib)
}

task swiftSwiftPackageManagerPublish(type: SourceCopyTask) {
    sampleDir = file("../swift/swift-package-manager-publish")

    swiftPMProject("app", "App").fromTemplate("swift-app")

    project("release-plugin").buildRoot()
    project("release-plugin").fromTemplate("release-plugin")

    libProject("list-library").buildRoot()
    libProject("list-library").fromTemplate("swift-list-lib-build-with-release")
    libProject("list-library").fromTemplate(swiftListLib)

    libProject("utilities-library").buildRoot()
    libProject("utilities-library").fromTemplate("swift-utilities-lib-build-with-release")
    libProject("utilities-library").fromTemplate(swiftUtilsLib)
}

task generateCppListRepo(type: GitRepoTask) {
    dependsOn cppSourceDependencies
    sampleDir = file("../cpp/source-dependencies/repos/list-library")
    changes["1.0"] = { destDir ->
        changeContent("src/main/cpp/get.cpp", "index > 0", "index > 1")
        "introduce bug in get.cpp"
    }
    changes["1.1"] = { destDir ->
        changeContent("build.gradle", "version = '1.0'", "version = '1.1'")
        changeContent("src/main/cpp/get.cpp", "index > 1", "index > 0")
        "fix bug in get.cpp"
    }
    changes["2.0"] = { destDir ->
        changeContent("build.gradle", "version = '1.1'", "version = '2.0'")
        "major release"
    }
    changes["SNAPSHOT"] = { destDir ->
        changeContent("build.gradle", "version = '2.0'", "version = '2.0-SNAPSHOT'")
        "work in progress"
    }
}

task generateCppUtilitiesRepo(type: GitRepoTask) {
    dependsOn cppSourceDependencies
    sampleDir = file("../cpp/source-dependencies/repos/utilities-library")
    changes["1.0"] = {
        "initial version"
    }
    changes["1.1"] = { destDir ->
        changeContent("build.gradle", "version = '1.0'", "version = '1.1'")
        changeContent("build.gradle", "list:1.0", "list:1.+")
        "use latest list 1.x library"
    }
    changes["2.0"] = { destDir ->
        changeContent("build.gradle", "version = '1.1'", "version = '2.0'")
        changeContent("build.gradle", "list:1.+", "list:2.+")
        "require list 2.0"
    }
    changes["SNAPSHOT"] = { destDir ->
        changeContent("build.gradle", "version = '2.0'", "version = '2.0-SNAPSHOT'")
        "work in progress"
    }
}

task generateCppRepos {
    dependsOn generateCppListRepo, generateCppUtilitiesRepo
}

task generateSwiftListRepo(type: GitRepoTask) {
    dependsOn swiftSourceDependencies
    sampleDir = file("../swift/source-dependencies/repos/list-library")
    changes["1.0"] = { destDir ->
        changeContent("src/main/swift/LinkedList.swift", "c > 0", "c > 1")
        "introduce bug in LinkedList"
    }
    changes["1.1"] = { destDir ->
        changeContent("build.gradle", "version = '1.0'", "version = '1.1'")
        changeContent("src/main/swift/LinkedList.swift", "c > 1", "c > 0")
        "fix bug in LinkedList"
    }
    changes["2.0"] = { destDir ->
        changeContent("build.gradle", "version = '1.1'", "version = '2.0'")
        "major release"
    }
    changes["SNAPSHOT"] = { destDir ->
        changeContent("build.gradle", "version = '2.0'", "version = '2.0-SNAPSHOT'")
        "work in progress"
    }
}

task generateSwiftUtilitiesRepo(type: GitRepoTask) {
    dependsOn swiftSourceDependencies
    sampleDir = file("../swift/source-dependencies/repos/utilities-library")
    changes["1.0"] = {
        "initial version"
    }
    changes["1.1"] = { destDir ->
        changeContent("build.gradle", "version = '1.0'", "version = '1.1'")
        changeContent("build.gradle", "list:1.0", "list:1.+")
        "use latest list 1.x library"
    }
    changes["2.0"] = { destDir ->
        changeContent("build.gradle", "version = '1.1'", "version = '2.0'")
        changeContent("build.gradle", "list:1.+", "list:2.+")
        "require list 2.0"
    }
    changes["SNAPSHOT"] = { destDir ->
        changeContent("build.gradle", "version = '2.0'", "version = '2.0-SNAPSHOT'")
        "work in progress"
    }
}

task generateSwiftRepos {
    dependsOn generateSwiftListRepo, generateSwiftUtilitiesRepo
}

task generateMinimalSwiftListRepo(type: GitRepoTask) {
    dependsOn swiftInjectedPlugins
    sampleDir = file("../swift/injected-plugins/repos/list-library")
    changes["1.0"] = { destDir ->
        "initial version"
    }
}

task generateMinimalSwiftUtilitiesRepo(type: GitRepoTask) {
    dependsOn swiftInjectedPlugins
    sampleDir = file("../swift/injected-plugins/repos/utilities-library")
    changes["1.0"] = {
        "initial version"
    }
}

task generateMinimalSwiftRepos {
    dependsOn generateMinimalSwiftListRepo, generateMinimalSwiftUtilitiesRepo
}

task generatePublishSwiftReleasePluginRepo(type: GitRepoTask) {
    dependsOn swiftSwiftPackageManagerPublish
    sampleDir = file("../swift/swift-package-manager-publish/release-plugin")
    changes["1.0"] = {
        "initial version"
    }
}

task generatePublishSwiftListRepo(type: GitRepoTask) {
    dependsOn swiftSwiftPackageManagerPublish
    sampleDir = file("../swift/swift-package-manager-publish/list-library")
}

task generatePublishSwiftUtilitiesRepo(type: GitRepoTask) {
    dependsOn swiftSwiftPackageManagerPublish
    sampleDir = file("../swift/swift-package-manager-publish/utilities-library")
}

class SourceCopyTask extends DefaultTask {
    final DirectoryProperty sampleDir = project.layout.directoryProperty()
    final DirectoryProperty templatesDir = project.layout.directoryProperty()
    final Map<String, TemplateTarget> projects = [:]

    @TaskAction
    def go() {
        def cleaned = new HashSet()
        projects.values().each { p ->
            def modules = p.includedModules.collect { "import $it" as String }
            def testModules = p.includedModules.collect { "@testable import $it" as String }
            def sourceBuilder = new SourceBuilder(p.module, modules, testModules, cleaned)
            p.visitDirs(sourceBuilder)
        }
    }

    GradleTarget project(String projectDir) {
        return add(new GradleTarget(projectDir))
    }

    AppTarget appProject(String projectDir) {
        return add(new AppTarget(projectDir))
    }

    LibTarget libProject(String projectDir) {
        return add(new LibTarget(projectDir))
    }

    StaticLibTarget staticLibProject(String projectDir) {
        return add(new StaticLibTarget(projectDir))
    }

    SwiftPmTarget swiftPMProject(String projectDir, String targetName) {
        return add(new SwiftPmTarget(projectDir, targetName))
    }

    private def add(TemplateTarget target) {
        if (projects.containsKey(target.key)) {
            return projects.get(target.projectDir)
        }
        projects.put(target.key, target)
        return target
    }

    static abstract class TemplateTarget {
        final List<Template> templates = []
        final String projectDir

        TemplateTarget(String projectDir) {
            this.projectDir = projectDir
        }

        TemplateTarget fromTemplate(String templateName) {
            return fromTemplate(Template.of(templateName))
        }

        TemplateTarget fromTemplate(Template template) {
            templates.add(template)
            return this
        }

        String getModule() {
            def libs = templates.findAll { it instanceof SwiftLibraryTemplate }
            return libs.empty ? null : libs.first().module
        }

        List<String> getIncludedModules() {
            def libs = templates.findAll { it instanceof SwiftLibraryTemplate }
            return libs.collect { it.module }
        }

        abstract String getKey()

        /**
         * Visits each src, dest directory pair for this project.
         * @param cl
         */
        void visitDirs(SourceBuilder builder) {
            templates.each { template ->
                builder.copyDir(template.templateName, projectDir)
                visitDirMappings(template) { src, dest, lineFilter ->
                    builder.copyDirTree("${template.templateName}/${src}", "${projectDir}/${dest}", lineFilter)
                }
            }
        }

        Closure addDllExportToPublicHeader(Template template) {
            if (template instanceof CppLibraryTemplate) {
                def macroName = template.name.toUpperCase() + "_API"
                def exportMacroName = template.name.toUpperCase() + "_MODULE_EXPORT"
                def macroDef = "#define ${macroName}"

                return { line ->
                    if (line == macroDef) {
                        return """
#ifdef _WIN32
#  ifdef ${exportMacroName}
#    define ${macroName} __declspec(dllexport)
#  else
#    define ${macroName} __declspec(dllimport)
#  endif
#else
#  define ${macroName}
#endif                        
""".trim()
                    }
                    return line
                }
            }
            return null
        }

        /**
         * Visits each candidate src, dest directory pair for this project and the given template.
         */
        abstract void visitDirMappings(Template template, Closure cl)
    }

    class SourceBuilder {
        final Set<File> cleaned
        final String module
        final Collection<String> modules
        final Collection<String> testModules

        SourceBuilder(String module, Collection<String> modules, Collection<String> testModules, Set<File> cleaned) {
            this.module = module
            this.cleaned = cleaned
            this.modules = modules
            this.testModules = testModules
        }

        /**
         * Returns the relative path from the sample dir to the root of the samples source tree.
         */
        String relativePathTo(String targetDirName) {
            return sampleDir.dir(targetDirName).get().asFile.toPath().relativize(project.projectDir.parentFile.toPath()).toString()
        }

        File getTemplateFile(String templateFileName) {
            return templatesDir.file(templateFileName).get().asFile
        }

        File writeTargetFile(String targetFileName, String content) {
            def file = sampleDir.file(targetFileName).get().asFile
            file.parentFile.mkdirs()
            file.text = content
            return file
        }

        void copyDir(String templateDirName, String targetDirName) {
            copy(templateDirName, targetDirName, null, false)
        }

        void copyDirTree(String templateDirName, String targetDirName, Closure lineFilter) {
            copy(templateDirName, targetDirName, lineFilter, true)
        }

        private void copy(String templateDirName, String targetDirName, Closure lineFilter, boolean recursive) {
            def srcDir = templatesDir.dir(templateDirName).get().asFile
            if (!srcDir.directory || srcDir.list().length == 0) {
                return
            }
            def destDir = sampleDir.dir(targetDirName).get().asFile
            if (recursive && cleaned.add(destDir)) {
                // TODO - generate the test main.swift
                project.delete project.fileTree(destDir, { exclude '**/main.swift' })
            }
            project.copy {
                from srcDir
                into destDir
                includeEmptyDirs = false
                if (!recursive) {
                    include '*'
                }
                if (lineFilter) {
                    filter(lineFilter)
                }
                filter { line ->
                    if (modules.contains(line)) {
                        return null
                    }
                    if (testModules.contains(line)) {
                        return "@testable import " + module
                    }
                    line
                }
            }
        }
    }

    static class GradleTarget extends TemplateTarget {
        boolean rootDir

        GradleTarget(String projectDir) {
            super(projectDir)
        }

        GradleTarget buildRoot() {
            rootDir = true
            return this
        }

        @Override
        void visitDirs(SourceBuilder sourceBuilder) {
            if (rootDir) {
                def relPath = sourceBuilder.relativePathTo(projectDir)
                def bashContent = sourceBuilder.getTemplateFile("build-root/gradlew").text
                bashContent = bashContent.replace("REL_PATH", relPath)
                def bashFile = sourceBuilder.writeTargetFile("${projectDir}/gradlew", bashContent)
                bashFile.setExecutable(true)
                def batContent = sourceBuilder.getTemplateFile("build-root/gradlew.bat").text
                batContent = batContent.replace("REL_PATH", relPath.replace("/", "\\"))
                sourceBuilder.writeTargetFile("${projectDir}/gradlew.bat", batContent)
            }
            super.visitDirs(sourceBuilder)
        }

        @Override
        String getKey() {
            return projectDir
        }

        @Override
        void visitDirMappings(Template template, Closure cl) {
            cl.call("src/main/java", "src/main/java", null)
            cl.call("src/main/groovy", "src/main/groovy", null)
        }
    }

    static class AppTarget extends GradleTarget {
        AppTarget(String projectDir) {
            super(projectDir)
        }

        @Override
        String getModule() {
            return "App"
        }

        @Override
        void visitDirMappings(Template template, Closure cl) {
            cl.call("src/main/cpp", "src/main/cpp", null)
            cl.call("src/main/headers", "src/main/headers", null)
            cl.call("src/main/public", "src/main/headers", null)
            cl.call("src/main/swift", "src/main/swift", null)
            cl.call("src/test/swift", "src/test/swift", null)
        }
    }

    static class LibTarget extends GradleTarget {
        LibTarget(String projectDir) {
            super(projectDir)
        }

        @Override
        void visitDirMappings(Template template, Closure cl) {
            cl.call("src/main/cpp", "src/main/cpp", null)
            cl.call("src/main/headers", "src/main/headers", null)
            cl.call("src/main/public", "src/main/public", addDllExportToPublicHeader(template))
            cl.call("src/main/swift", "src/main/swift", null)
            cl.call("src/test/swift", "src/test/swift", null)
        }
    }

    static class StaticLibTarget extends GradleTarget {
        StaticLibTarget(String projectDir) {
            super(projectDir)
        }

        @Override
        void visitDirMappings(Template template, Closure cl) {
            cl.call("src/main/cpp", "src/main/cpp", null)
            cl.call("src/main/headers", "src/main/headers", null)
            cl.call("src/main/public", "src/main/public", null)
            cl.call("src/main/swift", "src/main/swift", null)
            cl.call("src/test/swift", "src/test/swift", null)
        }
    }

    static class SwiftPmTarget extends TemplateTarget {
        final String targetName

        SwiftPmTarget(String projectDir, String targetName) {
            super(projectDir)
            this.targetName = targetName
        }

        @Override
        String getModule() {
            return targetName
        }

        @Override
        String getKey() {
            return "$projectDir:$targetName"
        }

        @Override
        void visitDirMappings(Template template, Closure cl) {
            cl.call("src/main/cpp", "Sources/${targetName}", null)
            cl.call("src/main/headers", "Sources/${targetName}/include", null)
            cl.call("src/main/public", "Sources/${targetName}/include", addDllExportToPublicHeader(template))
            cl.call("src/main/swift", "Sources/${targetName}", null)
            cl.call("src/test/swift", "Tests/${targetName}Tests", null)
        }
    }
}

class Template {
    final String templateName

    Template(String templateName) {
        this.templateName = templateName
    }

    static of(String templateName) {
        return new Template(templateName)
    }
}

class SwiftLibraryTemplate extends Template {
    final String module

    SwiftLibraryTemplate(String templateName, String module) {
        super(templateName)
        this.module = module
    }

    static of(String templateName, String module) {
        return new SwiftLibraryTemplate(templateName, module)
    }
}

class CppLibraryTemplate extends Template {
    final String name

    CppLibraryTemplate(String templateName, String name) {
        super(templateName)
        this.name = name
    }

    static of(String templateName, String name) {
        return new CppLibraryTemplate(templateName, name)
    }
}

class GitRepoTask extends DefaultTask {
    final DirectoryProperty sampleDir = project.layout.directoryProperty()

    Map<String, Closure> changes = [:]

    void changeContent(String srcFileName, String pattern, String replacement) {
        def srcFile = sampleDir.file(srcFileName).get().asFile
        def original = srcFile.text
        if (!original.find(pattern)) {
            throw new IllegalArgumentException("Source file ${srcFile} does not contain anything that matches '${pattern}'.")
        }
        srcFile.text = original.replace(pattern, replacement)
    }

    @TaskAction
    def go() {
        def destDir = sampleDir.get().asFile
        project.delete(new File(destDir, ".git"))
        InitCommand init = Git.init();
        Git git = init.setDirectory(destDir).call()
        try {
            new File(destDir, ".gitignore") << """
/.gradle
build
/.build
"""
            def files = []
            project.fileTree(destDir).visit { f ->
                if (f.file.file) {
                    files.add(f.relativePath)
                }
            }
            def add = git.add()
            add.addFilepattern(".gitignore")
            files.each {
                add.addFilepattern(it.pathString)
            }
            add.call()

            changes.each { tag, change ->
                def message = change(destDir)
                git.commit().setAll(true).setMessage(message).call()
                if (tag != 'SNAPSHOT') {
                    git.tag().setName(tag).call()
                }
            }
            if (changes.isEmpty()) {
                git.commit().setAll(true).setMessage("initial version").call()
            }
        } finally {
            git.close()
        }
    }
}
