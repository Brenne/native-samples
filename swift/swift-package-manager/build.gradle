import org.gradle.internal.os.OperatingSystem
def maybeExcludeLinuxMain(FileTree files) {
  if (OperatingSystem.current().macOsX) {
    return files.matching { exclude 'main.swift' }
  }
  return files
}

allprojects {
    apply plugin: 'xcode'
    apply plugin: 'xctest'
}

project(':app') {
    apply plugin:  'swift-executable'

    // Configure the source files of the executable
    executable {
        source.from rootProject.file('Sources/App')
    }
    
    xctest {
        source.from rootProject.file('Tests/AppTests')
    }

    // Configure the dependencies of the executable
    dependencies {
        implementation project(':greeter')
    }
}

project(':greeter') {
    apply plugin:  'swift-library'

    // Configure the source files of the library
    library {
        source.from rootProject.file('Sources/Greeter')
    }

    xctest {
        source.from maybeExcludeLinuxMain(rootProject.fileTree(dir: 'Tests/GreeterTests', include: '**/*.swift'))
    }
}

// Move the build directories for all projects into a single top-level directory
allprojects { p ->
    buildDir = rootProject.file("build/${p.name}")
}
