import org.gradle.internal.os.OperatingSystem

plugins {
    id 'swift-application'
    id 'xcode'
}

def greeterDebugImportPath = file('../simple-library/build/modules/main/debug/Math.swiftmodule')
def greeterReleaseImportPath = file('../simple-library/build/modules/main/release/Math.swiftmodule')

def greeterDebugLib = file(OperatingSystem.current().getSharedLibraryName('../simple-library/build/lib/main/debug/Math'))
def greeterReleaseLib = file(OperatingSystem.current().getSharedLibraryName('../simple-library/build/lib/main/release/Math'))

compileDebugSwift.doFirst {
    if (!greeterDebugLib.exists() || !greeterReleaseLib.exists() || !greeterDebugImportPath.exists() || !greeterReleaseImportPath.exists()) {
        throw new GradleException("Please build project at ${file('../simple-library')}.")
    }
}

dependencies {
    swiftCompileDebug files(greeterDebugImportPath)
    swiftCompileRelease files(greeterReleaseImportPath)
    nativeLinkDebug files(greeterDebugLib)
    nativeLinkRelease files(greeterReleaseLib)
    nativeRuntimeDebug files(greeterDebugLib)
    nativeRuntimeRelease files(greeterReleaseLib)
}
