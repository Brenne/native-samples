import org.gradle.samples.tasks.DownloadZipAndUnpack

// Currently cannot use the plugins { } block to reference plugins in included builds
// Use the buildscript { } block instead
buildscript {
    dependencies {
        classpath "org.gradle.samples:cmake:1.0"
    }
}

project(':app') {
    // TODO: Remove this workaround with new nightly
    tasks.withType(CppCompile) {
        dependsOn ":curl:downloadAndExtractCurl"
    }
}
project(':curl') {
    apply plugin: "lifecycle-base"

    // Apply the autotools-library plugin explicitly as we can't use the plugins { } block
    apply plugin: 'org.gradle.samples.autotools-library'

    ext.curlVersion = "7.58.0"

    task downloadAndExtractCurl(type: DownloadZipAndUnpack) {
        url.set "https://curl.haxx.se/download/curl-${curlVersion}.zip".toString()
        outputDirectory.set layout.buildDirectory.dir("curl-source")

        ext.headerDir = newOutputDirectory()
        headerDir.set outputDirectory.dir("curl-source/curl-${curlVersion}/include")

        ext.sourceBase = newOutputDirectory()
        sourceBase.set outputDirectory.dir("curl-${curlVersion}")
    }

    autotools {
        sourceDirectory = downloadAndExtractCurl.sourceBase
        includeDirectory = downloadAndExtractCurl.headerDir
        binary = 'lib/libcurl.a'

        // Additional arguments to pass to the 'configure' command
        configureArguments.add("--disable-ldap")
        configureArguments.add("--without-zlib")
        configureArguments.add("--without-libidn2")

        // Additional arguments to pass to the 'make' command
        makeArguments.add("-j4")
    }
}

allprojects {
    apply plugin: 'xcode'
}

