allprojects {
    apply plugin: 'xcode'
}

project(':app') {
    apply plugin: 'cpp-application'

    // Configure the source files of the executable
    application {
        source.from rootProject.file('Sources/App')

        dependencies {
            implementation project(':utilities')
        }
    }
}

project(':utilities') {
    apply plugin: 'cpp-library'

    // Configure the source files of the library
    library {
        source.from rootProject.file('Sources/Utilities')
        publicHeaders.from rootProject.file('Sources/Utilities/include')

        dependencies {
            api project(':list')
        }
    }
}

project(':list') {
    apply plugin: 'cpp-library'

    // Configure the source files of the library
    library {
        source.from rootProject.file('Sources/List')
        publicHeaders.from rootProject.file('Sources/List/include')
    }
}

// Move the build directories for all projects into a single top-level directory
allprojects { p ->
    buildDir = rootProject.file("build/${p.name}")

    // Work around for https://github.com/gradle/gradle-native/issues/376
    tasks.withType(AbstractLinkTask) {
        doFirst {
            p.projectDir.mkdirs()
        }
    }
}
