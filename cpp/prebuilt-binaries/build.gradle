import org.gradle.internal.os.OperatingSystem

plugins {
    id 'cpp-executable'
    id 'xcode'
}

def greeterIncludePath = file('../simple-library/src/main/public')

def greeterDebugLib = file(OperatingSystem.current().getSharedLibraryName('../simple-library/build/lib/main/debug/math'))
def greeterReleaseLib = file(OperatingSystem.current().getSharedLibraryName('../simple-library/build/lib/main/release/math'))

def greeterDebugLinkLib = file(OperatingSystem.current().getLinkLibraryName('../simple-library/build/lib/main/debug/math'))
def greeterReleaseLinkLib = file(OperatingSystem.current().getLinkLibraryName('../simple-library/build/lib/main/release/math'))

compileDebugCpp.doFirst {
    if (!greeterDebugLib.exists() || !greeterReleaseLib.exists()) {
        throw new GradleException("Please build project at ${file('../simple-library')} with: './gradlew linkRelease linkDebug'")
    }
    if (!greeterIncludePath.exists()) {
        throw new GradleException("Please ensure in include path: ${greeterIncludePath} exists")
    }
}

dependencies {
    cppCompileDebug files(greeterIncludePath)
    cppCompileRelease files(greeterIncludePath)
    nativeLinkDebug files(greeterDebugLinkLib)
    nativeLinkRelease files(greeterReleaseLinkLib)
    nativeRuntimeDebug files(greeterDebugLib)
    nativeRuntimeRelease files(greeterReleaseLib)
}
